plugins {
    id 'java'
    id 'application'
}

import java.util.regex.Matcher;

dependencies {
    implementation project(':decoder')

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

ext.moduleName = 'de.avwc.app'

// tag::run[]
mainClassName = "$moduleName/de.avwc.app.Spiegel"

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName
        ]
        classpath = files()
    }
}
// end::run[]

// tag::startScripts[]
startScripts {
    inputs.property("moduleName", moduleName)
    doFirst {
        classpath = files()
        defaultJvmOpts = [
                '--module-path', 'APP_HOME_LIBS',  // <1>
                '--module', mainClassName
        ]
    }
    doLast{
        def bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('$APP_HOME/lib'))

        def batFile = new File(outputDir, applicationName + ".bat")
        String batContent = batFile.text
        batFile.text = batContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('%APP_HOME%\\lib'))
    }
}
// end::startScripts[]

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

/*
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }

    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    }

}
*/